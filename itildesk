#!/usr/bin/env perl

use Mojolicious::Lite;

########################################################################
### Create a New Game
########################################################################

our $_game;

# Create people seeded with random skill level
#
sub new_people {
  map { $_ => { skill => rand }}
  qw(
    Jack William Hiroto Ren Wei Jie Aarav Aaryan Jing Ying Aadhya Ananya
    Yuina Hina Ben Louis Emma Mia Charlotte Ruby Michelle Shane Nicholas
  )
}

# Create desks with abilities
#
sub new_desks {
  service => {
    can => {
      identify => 1,
      request  => 'engineering',
      assign   => 1,
      handover => 'deploy',
    },
  },
  incident => {
    can => {
      identify => 1,
      request  => 'incident',
      invent   => 1,
      breakfix => 1,
      pass     => 'problem',
    },
  },
  problem => {
    can => {
      identify   => 1,
      request    => 'engineering',
      workaround => 1,
      handover   => 'deploy',
    },
  },
  deploy => {
    can => {
      apply => 1,
    },
  };
}

# Create issue seeded with difficulty and frequency
#
sub new_issue_types {
  map { $_ => { frequency => rand, difficulty => rand } }
   'Kernel Hung',
   'Disk Full',
   'User Cannot Login',
   'Package Outdated',
}

# Create type of issues seeded with difficulty and frequency
#
sub new_service_types {
  map { $_ => { frequency => rand, difficulty => rand } }
   'Hosting',
   'Decom',
   'Change',
}

# Create a new randomly seeded game
#
sub new_game {
  my %desks = new_desks();
  my %people = new_people();
  my @desknames = keys %desks;
  #use Data::Printer; p %desks;

  # Randomly distribute people to desks
  #
  while ( my($name,$attr) = each %people ) {
    # Random desk
    my $deskname = $desknames[rand @desknames];
    my $desk = $desks{$deskname};
    $desk->{person}{$name} = $attr;
  }

  $_game->{desks} = \%desks;
  $_game->{issue_types} = { new_issue_types() };
  $_game->{service_types} = { new_service_types() };
}

########################################################################
### Tickets
########################################################################

# Add a new issue into a desk
#
our $_id_counter;
sub create_issue {
  my($deskname) = @_;

  # Pick ticket type based on frequency
  my $type = (
    map $_->[0],
    sort { $b->[1] <=> $a->[1] }
    map [ $_, $_game->{issue_types}{$_}{frequency}*rand ],
    keys %{$_game->{issue_types}}
  )[0];
  # Create ticket
  my $ticket = {
    request    => 'issue',
    solution   => $type,
    difficulty => $_game->{issue_types}{$type}{difficulty},
  };
  # Place ticket on desk
  $_game->{desks}{$deskname}{ticket}{++$_id_counter} = $ticket;
}

# Add a new issue into a desk
#
sub create_service {
  my($deskname) = @_;

  # Pick ticket based on frequency
  my $type = (
    map $_->[0],
    sort { $b->[1] <=> $a->[1] }
    map [ $_, $_game->{service_types}{$_}{frequency}*rand ],
    keys %{$_game->{service_types}}
  )[0];
  # Create ticket
  my $ticket = {
    request    => 'service',
    solution   => $type,
    difficulty => $_game->{service_types}{$type}{difficulty},
  };
  # Place ticket on desk
  $_game->{desks}{$deskname}{ticket}{++$_id_counter} = $ticket;
}

# Pass a ticket into another queue
#
sub pass {
  my($deskname,$ticket_id) = @_;

  # Find new desk
  my $target_deskname = $_game->{desks}{$deskname}{can}{pass};
  return {error=>"$deskname desk cannot pass tickets"} unless $target_deskname;

  # Find ticket
  my $ticket = $_game->{desks}{$deskname}{ticket}{$ticket_id};
  return {error=>"Ticket $ticket_id is not on $deskname desk"} unless $ticket;

  # Remove current assignment TODO

  # Remove from desk
  delete $_game->{desks}{$deskname}{ticket}{$ticket_id};

  # Add to desk
  $_game->{desks}{$target_deskname}{ticket}{$ticket_id} = $ticket;
  
}

########################################################################
### Web Server
########################################################################

app->secrets(['litedesk secret passphrase goes here']);

new_game();
create_issue('incident') for 1..5;
create_service('service') for 1..5;
pass('incident', 1);

post '/pass' => sub {
  my $c = shift;
   my $resp = pass( $c->param('desk'), $c->param('ticket') );
   $c->flash( error => $resp->{error} ) if ref $resp;
   $c->redirect_to('index');
} => 'pass';

get '/' => sub {
  my $c = shift;
  $c->stash( game => $_game );
  $c->render(template => 'index');
} => 'Ã­ndex';

app->start;
